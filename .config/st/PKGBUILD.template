# Maintainer: Jose Riha <jose1711 gmail com>
# Maintainer: Sebastian J. Bronner <waschtl@sbronner.com>
# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Contributor: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.8.2
pkgrel=10
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=(libxft)
url=https://st.suckless.org
source=(https://dl.suckless.org/$pkgname/$pkgname-$pkgver.tar.gz
        terminfo.patch
        README.terminfo.rst)
_sourcedir=$pkgname-$pkgver
_makeopts="--directory=$_sourcedir"

_patches=(
#    "https://st.suckless.org/patches/clipboard/st-clipboard-0.8.2.diff"
#    "https://st.suckless.org/patches/copyurl/st-copyurl-20190202-0.8.1.diff"
#    "https://st.suckless.org/patches/copyurl/st-copyurl-20190202-3be4cf1.diff"
    "https://st.suckless.org/patches/selectioncolors/st-selectioncolors-0.8.2.diff"
    "https://st.suckless.org/patches/scrollback/st-scrollback-0.8.2.diff"
    "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-0.8.2.diff"
    "https://st.suckless.org/patches/anysize/st-anysize-0.8.1.diff"
#    "https://st.suckless.org/patches/scrollback/st-scrollback-20190331-21367a0.diff"
#    "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-20191024-a2c479c.diff"
#    "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-20191024-a2c479c.diff"
#    "https://st.suckless.org/patches/scrollback/st-scrollback-mouse-increment-0.8.2.diff"
#    "https://st.suckless.org/patches/vimBrowse/st-vimBrowse-20191107-2b8333f.diff"
    "dwm-custom.diff"
    )
source=(${source[@]} ${_patches[@]})

prepare() {
  echo "installing patches"
  patch --directory="$_sourcedir" --strip=0 < terminfo.patch
  cd "$srcdir/$pkgname-$pkgver"

  for patch in "${_patches[@]}"; do
    if [ ${patch: -5} == ".diff" ] ; then
      echo "Applying patch $(basename $patch)..."
      patch -Np1 -i "$srcdir/$(basename $patch)"
    elif [ ${patch: -2} == ".c" ] ; then
      echo "Copying c file $(basename $patch)..."
      cp $srcdir/$(basename $patch) .
    fi

  done
  echo "installed patches"
}

build() {
  make $_makeopts X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  local installopts='--mode 0644 -D --target-directory'
  local shrdir="$pkgdir/usr/share"
  local licdir="$shrdir/licenses/$pkgname"
  local docdir="$shrdir/doc/$pkgname"
  make $_makeopts PREFIX=/usr DESTDIR="$pkgdir" install
  install $installopts "$licdir" "$_sourcedir/LICENSE"
  install $installopts "$docdir" "$_sourcedir/README"
  install $installopts "$docdir" README.terminfo.rst
  install $installopts "$shrdir/$pkgname" "$_sourcedir/st.info"
}
